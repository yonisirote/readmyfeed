// Top-level build file where you can add configuration options common to all sub-projects/modules.

buildscript {
    
    repositories {
        google()
        mavenCentral()
    }
    dependencies {
        classpath 'com.android.tools.build:gradle:8.13.0'
        classpath 'com.google.gms:google-services:4.4.4'

        // NOTE: Do not place your application dependencies here; they belong
        // in the individual module build.gradle files
    }
}

apply from: "variables.gradle"

allprojects {
    repositories {
        google()
        mavenCentral()
    }
}

task clean(type: Delete) {
    delete rootProject.buildDir
}

subprojects { project ->
    afterEvaluate {
        if ((project.plugins.hasPlugin('com.android.library') || project.plugins.hasPlugin('com.android.application')) && project.hasProperty('android')) {
            def android = project.android
            if (!android.namespace) {
                def manifest = project.file('src/main/AndroidManifest.xml')
                if (manifest.exists()) {
                    def parser = new groovy.xml.XmlSlurper()
                    def rootNode = parser.parse(manifest)
                    def pkg = rootNode.@package.text()
                    if (pkg) {
                        android.namespace = pkg
                        println "Applied namespace '${pkg}' to ${project.name}"
                    }
                }
            }
        }
    }
}
